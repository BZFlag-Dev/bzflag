# Process this file with autoconf to produce a configure script.

AC_INIT(src/bzflag/bzflag.cxx)

# sed magic to get version numbers from include/version.h
MAJOR_VERSION=`sed -e 's/#.*define.*BZ_MAJOR_VERSION[^0-9]*\(.*\)/\1/' -e t -e d < \$srcdir/include/version.h`
MINOR_VERSION=`sed -e 's/#.*define.*BZ_MINOR_VERSION[^0-9]*\(.*\)/\1/' -e t -e d < \$srcdir/include/version.h`
REV=`sed -e 's/#.*define.*BZ_REV[^0-9]*\(.*\)/\1/' -e t -e d < \$srcdir/include/version.h`

CONF_DATE=`date +%Y%m%d`
echo "BZFlag-$MAJOR_VERSION.$MINOR_VERSION.$REV.$CONF_DATE"
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(bzflag, $MAJOR_VERSION.$MINOR_VERSION.$REV.$CONF_DATE)
AM_CONFIG_HEADER(include/config.h)

#Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_CXX
dnl On SGI systems, figure out if we are using MIPSPro compiler
using_mipspro=no
if test "$build_vendor" = "sgi" ; then
    AC_MSG_CHECKING([whether we are using the MIPSPro compiler])
    AC_TRY_COMPILE([], 
		   [#if defined(sgi) && !defined(__GNUC__) && defined(_COMPILER_VERSION)
this line should cause a compile failure when building with the MIPSpro compiler;
#endif
int main() { return 0; }
    ], using_mipspro=no, using_mipspro=yes)
    AC_MSG_RESULT([$using_mipspro])
fi

AC_PROG_LN_S
AC_PROG_RANLIB
AC_CHECK_PROG(HASPOD2MAN, pod2man, yes, no)
if test $HASPOD2MAN = no; then
  AC_MSG_WARN([some man pages will not be generated])
fi
AM_CONDITIONAL(HASPOD2MAN, test $HASPOD2MAN = yes)
AC_SUBST(AR)
AC_CHECK_PROG(AR, ar, ar)
AC_SUBST(AR_FLAGS)

AC_CHECK_LIB(z, compress, BUILDZLIB=no, BUILDZLIB=yes)
AM_CONDITIONAL(BUILDZLIB, test $BUILDZLIB = yes)

# Just for kicks
echo -n "checking for artificial intelligence for robots..." 1>&6
echo " warning: not found" 1>&6

# Let the C++ code know about OS and user
AC_DEFINE_UNQUOTED(BZ_BUILD_OS, "$host_os", [BZFlag System Environment])
AC_CHECK_PROG(HASWHOAMI, whoami, yes, no)
if test $HASWHOAMI = yes; then
  AC_DEFINE_UNQUOTED(BZ_BUILD_USER, "`whoami`", [User who compiled BZFlag])
else
  AC_DEFINE_UNQUOTED(BZ_BUILD_USER, "anonymous", [User who compiled BZFlag])
fi

#Checks for headers
AC_STDC_HEADERS
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_CHECK_HEADERS( \
	cstdlib \
	cstdio \
	cstring \
)
AC_LANG_RESTORE
AC_CHECK_HEADERS( \
	sys/param.h \
	values.h \
)

#Checks for types
#AM_CHECK_TYPES(socklen_t)
#AC_CHECK_TYPES(socklen_t)
AC_CONST

AC_TRY_COMPILE([#include <sys/types.h>
		#include <sys/socket.h>],
	       [socklen_t len = 42; return 0;],
               ac_cv_type_socklen_t=yes,
               ac_cv_type_socklen_t=no)
if test $ac_cv_type_socklen_t != yes; then
    AC_DEFINE(socklen_t, int, [if socklen_t is not defined, provide something useful])
else
    AC_DEFINE(HAVE_SOCKLEN_T, 1, [if socklen_t is defined, make note of it])
fi


case $host_os in
     linux*|freebsd*) linux=true;;
     *) linux=false;;
esac
AM_CONDITIONAL(LINUX, $linux)
AM_CONDITIONAL(BEOS, test x$host_os = xbeos)
AM_CONDITIONAL(PC,    test x$host_vendor = xpc)
AM_CONDITIONAL(PPC,   test x$host_vendor = xppc)

case $host_os in
       solaris*) solaris=true;;
       *) solaris=false;;
     esac
AM_CONDITIONAL(SOLARIS, $solaris)
# Write code to detect platform specialization
sun_wspro=false
sun_cc=false
sun_gcc=false

case $host_os in
     irix*) irix=true;;
     *) irix=false;;
esac
AM_CONDITIONAL(IRIX, $irix)
AM_CONDITIONAL(BUGGY_MIPSPRO, test "x$using_mipspro" = "xyes")

case $host_os in
     cygwin|mingw32|windows**) win32=true;;
     *) win32=false;;
esac
AM_CONDITIONAL(WIN32, $win32)

case $host_os in
   macos|darwin*) macos=true;;
   *) macos=false;;
esac

AM_CONDITIONAL(MACOS, $macos)

#CONFIG is only used for irix packaging
case $host_os in
   irix*)
	CONFIG=irix-mips3;
        AC_SUBST(CONFIG)
        ;;
esac

AC_PATH_XTRA

LDFLAGS="$LDFLAGS $X_LIBS"
case $host_os in
  solaris*)
    GLIBS="-lresolv -laudio $GLIBS";
    LIBS="-lsocket -lnsl -lresolv";
    LDFLAGS="$LDFLAGS -L/usr/demo/SOUND/lib";;
  irix*)
    GLIBS="-lXsgivc -lX11 -laudio $GLIBS";;
  beos)
    GLIBS=" -lmedia -lgame $GLIBS";
#    LIBS="-lbind -lsocket -lbe";;
    LIBS="-lbe";;
esac;


# Check for functions
AC_CHECK_FUNCS([getrlimit atexit])


# for BeOS - old network stack don't have those libs ( move it in the case switch ?)
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(bind, gethostent)

AC_CHECK_LIB(m, sqrtf)

# check to see if we have adns available to do hostname lookups in server
AC_CHECK_LIB(adns, adns_init)

# avoid using X11 in BeOS and Mac OS X
case $host_os in
    macos|darwin*)
	;;
    beos*)
	;;
    *)
	if test x$no_x = x; then
	  GLIBS="$X_PRE_LIBS -lX11 $EXTRA_LIBS $GLIBS"
	fi

	AC_CHECK_LIB(Xext, XextAddDisplay, [GLIBS="-lXext $GLIBS"], [], $GLIBS)

	if test $host_os = linux-gnu; then
	  AC_CHECK_LIB(Xxf86vm, XF86VidModeGetAllModeLines, [GLIBS="-lXxf86vm $GLIBS"], [], $GLIBS)
	  AC_CHECK_LIB(Xi, XListInputDevices, [GLIBS="-lXi $GLIBS"], [], $GLIBS)
	fi
    ;;
esac

AC_LANG_CPLUSPLUS

AC_TRY_LINK([
#include <sys/types.h>
#include <regex.h>
], [
regcomp(0, 0, 0);
regexec(0, 0, 0, 0, 0);
],
[AC_DEFINE(HAVE_REGEX_H, 1, [Define to 1 if you have regex stuff available])], [])

ac_cv_search_glBegin=no
ac_cv_search_gluScaleImage=no
ac_func_search_save_LIBS=$LIBS

LIBS="-framework OpenGL $GLIBS $ac_func_search_save_LIBS"
AC_TRY_LINK([#include <OpenGL/gl.h>],
            [glBegin(GL_POINTS)],
            [ac_cv_search_glBegin="-framework OpenGL"])
for ac_lib in opengl32 GL2 GL; do
  LIBS="-l$ac_lib $GLIBS $ac_func_search_save_LIBS"
  AC_TRY_LINK([#include <GL/gl.h>],
              [glBegin(GL_POINTS)],
              [ac_cv_search_glBegin="-l$ac_lib"
               break])
done
if test "$ac_cv_search_glBegin" != no; then
  GLIBS="$ac_cv_search_glBegin $GLIBS"
  if test "$ac_cv_search_glBegin" = -lGL2; then
    AC_DEFINE(BEOS_USE_GL2, 1, [Use new GL Kit for BeOS])
  fi

  LIBS="-framework OpenGL $ac_func_search_save_LIBS"
  AC_TRY_LINK([#include <OpenGL/glu.h>],
              [gluScaleImage(GL_RED,0,0,GL_INT,0,0,0,GL_INT,0)],
              [ac_cv_search_gluScaleImage="-framework OpenGL"])
  for ac_lib in glu32 GLU GL; do
    LIBS="-l$ac_lib $GLIBS $ac_func_search_save_LIBS"
    AC_TRY_LINK([#include <GL/glu.h>],
                [gluScaleImage(GL_RED,0,0,GL_INT,0,0,0,GL_INT,0)],
		[ac_cv_search_gluScaleImage="-l$ac_lib"
		 break])
  done
  if test "$ac_cv_search_gluScaleImage" != no; then
    GLIBS="$ac_cv_search_gluScaleImage $GLIBS"
  fi
fi

LIBS=$ac_func_search_save_LIBS
AC_SUBST(GLIBS)

if test $host_os = mingw32; then
  LIBS="-lwinmm -lws2_32 $LIBS"
  GLIBS="-mwindows -lgdi32 -ldsound $GLIBS"
fi

# Remove ogg/vorbis dependencies until we actually need them.
#
# AC_CHECK_LIB(ogg, ogg_stream_init, [ALIBS="-logg $ALIBS"], [], $ALIBS)
# AC_CHECK_LIB(vorbis, vorbis_info_init, [ALIBS="-lvorbis $ALIBS"], [], $ALIBS)
# AC_CHECK_LIB(vorbisfile, ov_open, [ALIBS="-lvorbisfile $ALIBS"], [], $ALIBS)
# AC_SUBST(ALIBS)

case $host_os in
     macos|darwin*)
	LIBS="$LIBS -framework Carbon -framework OpenGL -framework AGL"
	;;
     irix)
	if test x$host_cpu = xmips64; then
	   LDFLAGS="$LDFLAGS -L$(ROOT)/usr/lib32"
	else
	   LDFLAGS="$LDFLAGS -L$(ROOT)/usr/lib"
	fi
	;;
esac
if test $prefix = NONE; then
	prefix=$ac_default_prefix
fi
INSTALL_DATA_DIR=`eval echo $datadir/bzflag`
AC_SUBST(INSTALL_DATA_DIR)

# do we want bzadmin?
AC_ARG_ENABLE(bzadmin, [  --disable-bzadmin       do not build text client])
if test x$enable_bzadmin != xno; then
  if test "$ac_cv_search_gluScaleImage" = no; then
    AC_MSG_WARN(
      [bzAdmin build has been requested, but GL is not fully available
... disabling bzAdmin generation])
    AM_CONDITIONAL(BZADMIN_INCLUDED, false)
  else
    MP_WITH_CURSES
    if test "x$CURSES_LIB" = x; then
      AC_MSG_WARN([could not find a curses library, will build bzadmin without curses])
    fi
    AM_CONDITIONAL(BZADMIN_INCLUDED, true)
  fi
else
  AM_CONDITIONAL(BZADMIN_INCLUDED, false)
fi
AM_CONDITIONAL(HAVE_CURSES, test "x$CURSES_LIB" != x)
AC_SUBST(CURSES_LIB)


AC_ARG_ENABLE(timebomb, [  --enable-timebomb       build exp Date])
if test x$enable_timebomb = xyes; then
   AC_DEFINE(TIME_BOMB, "3/13/1998", [Time Bomb expiration])
fi

savedLIBS=$LIBS
LIBS="$GLIBS $LIBS"
AC_ARG_WITH(SDL, [  --without-SDL              do not use Simple DirectMedia Layer])
if test x$with_SDL != xno; then
  with_SDL=yes
fi
AC_LANG_C
if test x$with_SDL = xyes; then
  AM_PATH_SDL(1.2.5,, with_SDL=no)
  if test x$with_SDL = xno; then
     AC_MSG_WARN([could not find an appropriate SDL version. \
BZflag will build without SDL])
  fi
fi
AC_LANG_CPLUSPLUS
LIBS=$savedLIBS
if test x$with_SDL = xyes; then
  AC_DEFINE(HAVE_SDL, 1, [Using SDL])
fi
AM_CONDITIONAL(HAVE_SDL, test x$with_SDL = xyes)

AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging])
AM_CONDITIONAL(DEBUG, test x$enable_debug = xyes)
if test x$enable_debug = xyes; then
   AC_DEFINE(DEBUG_RENDERING, 1, [Debug Rendering])
   AC_ARG_ENABLE(profiling, [  --enable-profiling      turn on profiling])
fi

AC_ARG_ENABLE(adns,  [  --enable-adns           use asynchronous DNS resolver],
[case "${enableval}" in
  yes) use_adns=true ;;
  no)  use_adns=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-adns) ;;
esac],[use_adns=false])
if $use_adns ; then
  AC_DEFINE([HAVE_ADNS], 1, [Define if ADNS is available])
fi

if test x$use_adns = xtrue ; then
AC_TRY_LINK([
#include <adns.h>],
[adns_init(0, (adns_initflags)0, 0)],
[AC_DEFINE(HAVE_ADNS_H, 1, [Define if you have ADNS header available])],
[AC_MSG_WARN([adns seems to not be available on your system.
See http://www.chiark.greenend.org.uk/~ian/adns/ if you want to be able to use hostmask based bans.])])
fi

AC_ARG_ENABLE(client, [  --disable-client        servers-only build])
if test x$enable_client != xno; then
  if test "$ac_cv_search_gluScaleImage" = no; then
    AC_MSG_WARN(
    [Client build has been requested, but GL is not fully available
     ... disabling client generation])
    AM_CONDITIONAL(CLIENT_INCLUDED, false)
  else
    AM_CONDITIONAL(CLIENT_INCLUDED, true)
  fi
else
  AM_CONDITIONAL(CLIENT_INCLUDED, false)
fi

AC_ARG_ENABLE(robots, [  --disable-robots        disable robots])
if test x$enable_robots != xno; then
  AC_DEFINE(ROBOT, 1, [Enabling Robots])
fi

AC_ARG_ENABLE(snapping, [  --disable-snapping      disable snapping])
if test x$enable_snapping != xno; then
  AC_DEFINE(SNAPPING, 1, [Enabling Snapping])
fi

AC_DEFINE_UNQUOTED(INSTALL_DATA_DIR, "$INSTALL_DATA_DIR",
[Data file directory])

case $host_os in
     linux-gnu)
	AC_DEFINE(HALF_RATE_AUDIO, 1, [Half rate Audio])
	AC_DEFINE(XF86VIDMODE_EXT, 1, [XFree86 Video Mode Extension])
	# FIXME see XIJoystick.cxx
	#if test x$no_sdl = xyes; then
	#AC_DEFINE(XIJOYSTICK, 1, [XInput Joystick Handling])
	#fi
	CPPFLAGS="$CPPFLAGS -Wall -W"
	case $host_vendor in
	     pc)  CPPFLAGS="$CPPFLAGS -mcpu=$host_cpu";;
	     ppc) CPPFLAGS="$CPPFLAGS -mcpu=$host_cpu";;
	esac;;
     cygwin)
	AC_DEFINE(HALF_RATE_AUDIO, 1, [Half rate Audio])
	AC_DEFINE(XF86VIDMODE_EXT, 1, [XFree86 Video Mode Extension])
	#if test x$no_sdl = xyes; then
	#AC_DEFINE(XIJOYSTICK, 1, [XInput Joystick Handling])
	#fi
	CPPFLAGS="$CPPFLAGS -Wall -W -mcpu=$host_cpu";;
     mingw32)
	AC_DEFINE(HALF_RATE_AUDIO, 1, [Half rate Audio])
	CPPFLAGS="$CPPFLAGS -Wall -W -mcpu=$host_cpu";;
     solaris*)
	AC_DEFINE(SUN_OGL_NO_VERTEX_MACROS, [], [Sun OpenGL No Macro Vertex])
	if $sun_wspro; then
	   CPPFLAGS="$CPPFLAGS -KPIC"
	elif $sun_cc; then
	   CPPFLAGS="$CPPFLAGS -fast"
	fi;;
     macos|darwin*)
	CPPFLAGS="$CPPFLAGS -pipe -ansi -pedantic -fno-exceptions -W -Wall -Wundef -Wstrict-prototypes";
	CFLAGS="$CFLAGS -pipe -pedantic -fno-exceptions -W -Wall -Wundef -Wstrict-prototypes";;
     irix)
	CPPFLAGS="$CPPFLAGS -Wall";
	if test x$host_cpu = xmips64; then
	   CPPFLAGS="CPPFLAGS -march=mips3";
	else
	   CPPFLAGS="CPPFLAGS -march=mips2";
	fi;
esac

if test x$enable_debug = xyes; then
   CFLAGS="$CFLAGS -O0";
   CXXFLAGS="$CXXFLAGS -O0";
   case $host_os in
     linux-gnu|cygwin|mingw32)
	CPPFLAGS="$CPPFLAGS -Werror";
	CXXFLAGS="$CXXFLAGS -Wno-deprecated";
	if test x$enable_profiling = xyes; then
	  CFLAGS="$CFLAGS -pg";
	  CXXFLAGS="$CXXFLAGS -pg";
	fi;;
     macos|darwin*)
	if test x$enable_profiling = xyes; then
	  CFLAGS="$CFLAGS -pg";
	  CXXFLAGS="$CXXFLAGS -pg";
	fi;;
     beos)
	CPPFLAGS="$CPPFLAGS -Werror";;
    esac;
    AC_DEFINE(DEBUG, 1, [Debugging enabled])
else
    case $host_os in
	 linux-gnu|cygwin|mingw32)
		CXXFLAGS="$CXXFLAGS -ffast-math -fno-exceptions";;
	 solaris*)
		CFLAGS="$CFLAGS -O";
		CXXFLAGS="$CXXFLAGS -O";;
	 macos|darwin*)
		CFLAGS="$CFLAGS -O3";
		CXXFLAGS="$CXXFLAGS -O3";
		CXXFLAGS="$CXXFLAGS -ffast-math -fomit-frame-pointer";
		CXXFLAGS="$CXXFLAGS -fexpensive-optimizations -fno-exceptions";
		if test x$enable_profiling = xyes; then
			CFLAGS="$CFLAGS -pg";
			CXXFLAGS="$CXXFLAGS -pg";
		fi;;
	 irix)
		if test x$host_cpu = xmips64; then
		   CFLAGS="$CFLAGS -O3";
		   CXXFLAGS="$CXXFLAGS -O3";
		   CPPFLAGS="$CPPFLAGS -ffast-math -fomit-frame-pointer";
		   CPPFLAGS="$CPPFLAGS -fexpensive-optimizations -fno-exceptions";
		else
		   CFLAGS="$CFLAGS -O2";
		   CXXFLAGS="$CXXFLAGS -O2";
		fi;;
	 beos)
		CFLAGS="$CFLAGS -O2";
		CXXFLAGS="$CXXFLAGS -O2";
		CXXFLAGS="$CXXFLAGS -ffast-math -fomit-frame-pointer -fno-exceptions";;
     esac;
     AC_DEFINE(NDEBUG, 1, [Debugging disabled])
fi;


# do we want to build our own regex lib
AC_ARG_WITH(regex, [  --with-regex            Use bzflag regex lib])
if test x$with_regex != xno ; then
   AC_DEFINE(BUILD_REGEX, 1, [Building regex])
fi;
AM_CONDITIONAL(BUILD_REGEX, test "x$with_regex" != "xno")


LIBDIR="\$(top_srcdir)/lib"
AC_SUBST(LIBDIR)

AC_OUTPUT(bzflag.info
	bzflag.lsm
	Dev-C++/Makefile
	Makefile
	bzflag.spec
	data/Makefile
	data/alternate/Makefile
	data/fonts/Makefile
	data/l10n/Makefile
	debian/Makefile
	doc/Makefile
	doc/faq/Makefile
	doc/guide/Commands/Makefile
	doc/guide/Flags/Makefile
	doc/guide/Geometry/Makefile
	doc/guide/Installation/Makefile
	doc/guide/Makefile
	doc/guide/Tactics/Makefile
	doc/howto/Makefile
	doc/man/Makefile
	include/Makefile
	misc/Makefile
	man/Makefile
	package/Makefile
	package/linux/Makefile
	package/rpm/Makefile
	package/irix/Makefile
	package/mac/Makefile
	package/mac/platform/Makefile
	package/mac/src/Makefile
	package/win32/Makefile
	package/win32/nsis/Makefile
	src/Makefile
	src/bzadmin/Makefile
	src/bzflag/Makefile
	src/bzfrelay/Makefile
	src/bzfs/Makefile
	src/common/Makefile
	src/date/Makefile
	src/3D/Makefile
	src/game/Makefile
	src/geometry/Makefile
	src/geometry/models/Makefile
	src/geometry/models/tank/Makefile
	src/mediafile/Makefile
	src/net/Makefile
	src/obstacle/Makefile
	src/ogl/Makefile
	src/platform/Makefile
	src/platform/MacOSX/Makefile
	src/platform/MacOSX/BZFlag.pbproj/Makefile
	src/platform/MacOSX/BZFlag.xcode/Makefile
	src/platform/MacOSX/English.lproj/Makefile
	src/platform/MacOSX/English.lproj/MainMenu.nib/Makefile
	src/regex/Makefile
	src/scene/Makefile
	src/zlib/Makefile
	win32/Makefile
	win32/VC6/Makefile
	win32/VC71/Makefile)
