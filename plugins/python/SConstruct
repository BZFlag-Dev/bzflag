#!/usr/bin/env python
from distutils import sysconfig
import sys

# Python library information - this is probably broken on non-posix OSes
python_lib = ['python%d.%d' % sys.version_info[0:2]]
python_libpath = [sysconfig.get_python_lib (0, 1) + '/config']
python_include = [sysconfig.get_python_inc ()]
python_linkflags = Split (sysconfig.get_config_var('LINKFORSHARED'))

files = [
	'PyBZDB.cpp',
	'PyBZFlag.cpp',
	'PyEvent.cpp',
	'PyEvents.cpp',
	'PyPlayer.cpp',
	'PyTeam.cpp',
	'PyWorldBox.cpp',
	'python.cpp',
	]

env = Environment ()
env.Append (CCFLAGS = '-g')

# include paths
env.Append (CPPPATH = python_include)
env.Append (CPPPATH = '../../include')

# library paths
env.Append (LIBPATH = python_libpath)

# extra flags
env.Append (LINKFLAGS = python_linkflags)

# libraries
env.Append (LIBS = python_lib)
env.Append (LIBS = 'pthread')
env.Append (LIBS = 'util')

env['SHLIBPREFIX'] = ''
env.SharedLibrary (target = 'python.so', source = files)
